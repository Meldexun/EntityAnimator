buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'

version = '1.12.2-1.0.0'
group = 'meldexun.entityanimator'
archivesBaseName = 'EntityAnimator'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runClient EA 1.12'
            environment 'MC_VERSION', '1.12.2'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'fml.coreMods.load', 'meldexun.entityanimator.asm.EntityAnimatorPlugin,lumien.chunkanimator.asm.LoadingPlugin'
        }

        server {
            taskName 'runServer EA 1.12'
            environment 'MC_VERSION', '1.12.2'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
        }
    }
}

configurations {
    includedDependencies
    implementation.extendsFrom(includedDependencies)
}

repositories {
    ivy {
        url 'https://github.com/'

        patternLayout {
            artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]'
        }

        metadataSources { artifact() } 
    }

    maven { url = 'https://www.cursemaven.com' }
    maven { url = 'https://repo.spongepowered.org/maven' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    includedDependencies 'meldexun:ReflectionUtil:1.0.0@jar'
    implementation 'curse.maven:ChunkAnimator-236484:2432424'

    includedDependencies('org.spongepowered:mixin:0.8.5') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, 'mixins.entityanimator.refmap.json'
    config 'mixins.entityanimator.json'
}

jar {
    from {
        configurations.includedDependencies.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'LICENSE.txt', 'META-INF/*.RSA'
    }

    manifest {
        attributes 'ModSide': 'CLIENT'
        attributes 'FMLCorePlugin': 'meldexun.entityanimator.asm.EntityAnimatorPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'ForceLoadAsMod': 'true'
        attributes 'FMLAT': 'accesstransformer.cfg'
        attributes 'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
        attributes 'TweakOrder': '0'
        attributes 'MixinConfigs': 'mixins.entityanimator.json'
    }
}
